
Statement      -> ClassStmt  | BlockStmt | LetStmt   | RepeatStmt | 
                  RequireStmt | IfStmt   | MacroStmt | PrintStmt  | 
                  ReturnStmt | ComparisonExpr
                 
ClassStmt      -> class IDENTIFIER DefineStmt

BlockStmt      -> do Statement* end

DefineStmt     -> define MemberStmt* end

MemberStmt     -> member IDENTIFIER BlockStmt |
                  member IDENTIFIER (= ComparisonExpr)?

LetStmt        -> let IDENTIFIER (= ComparisonExpr)?

RepeatStmt     -> repeat ComparisonExpr BlockStmt |
                  repeat ComparisonExpr (:|to) ComparisonExpr BlockStmt
                  
RequireStmt    -> require PrimaryValue

IfStmt         -> if ComparisonExpr BlockStmt (else BlockStmt)?

MacroStmt      -> macro IDENTIFIER BlockStmt

PrintStmt      -> print ComparisonExpr

ReturnStmt     -> return ComparisonExpr

ComparisonExpr -> ConditionExpr ((and|or) ConditionExpr)*

ConditionExpr  -> AdditionExpr  ((==|!=|<|<=|>|>=) AdditionExpr)*

AdditionExpr   -> MultiplyExpr  ((+|-) MultiplyExpr)*

MultiplyExpr   -> UnaryExpr     ((*|/) UnaryExpr)*

UnaryExpr      -> (!|-)? UnaryExpr | ( UnaryExpr ) | PrimaryExpr

PrimaryExpr    -> true | false | NUMBER | IDENTIFIER | STRING